playerWidth = 64
playerHeight = 64
blockSize = 64
heightMultiplier = 10
widthMultiplier = 15

levelFolderPath = Program.Directory + "\levels\"
moveSpeed = Math.Round(playerWidth / 5)
exceleration = Math.Round(playerHeight / 4)
keyboard["Left"] = 0
keyboard["Right"] = 0
keyboard["Up"] = 0

GraphicsWindow.Width = blockSize * widthMultiplier - 10
GraphicsWindow.Height = blockSize * heightMultiplier - 10
GraphicsWindow.CanResize = "False"
GraphicsWindow.Title = "Mario Game"
GraphicsWindow.KeyDown = OnKeyDown
GraphicsWindow.KeyUp = OnKeyUp
gh = GraphicsWindow.Height
gw = GraphicsWindow.Width

playerIdleRight = ImageList.LoadImage(Program.Directory + "\mario sprites\Idle right.png")
playerIdleLeft = ImageList.LoadImage(Program.Directory + "\mario sprites\Idle left.png")
playerWalkingRight = ImageList.LoadImage(Program.Directory + "\mario sprites\Walking right.png")
playerWalkingRight2 = ImageList.LoadImage(Program.Directory + "\mario sprites\Walking right 2.png")
playerWalkingLeft = ImageList.LoadImage(Program.Directory + "\mario sprites\Walking left 2.png")
playerWalkingLeft2 = ImageList.LoadImage(Program.Directory + "\mario sprites\Walking left.png")
playerInAirRight = ImageList.LoadImage(Program.Directory + "\mario sprites\In air right.png")
playerInAirLeft = ImageList.LoadImage(Program.Directory + "\mario sprites\In air left.png")
enemy1_1 = ImageList.LoadImage(Program.Directory + "\enemy sprites\Enemy 1 1.png")
enemy1_2 = ImageList.LoadImage(Program.Directory + "\enemy sprites\Enemy 1 2.png")
imageFiles = File.GetFiles(Program.Directory + "\tiles")
For i = 1 To Array.GetItemCount(imageFiles)
  imagePath = imageFiles[i]
  pos = Text.GetIndexOf(imagePath, "\")
  While pos > 0
    imagePath = Text.GetSubTextToEnd(imagePath, pos + 1)
    pos = Text.GetIndexOf(imagePath, "\")
  EndWhile
  
  tiles[Text.GetSubText(imagePath, 1, 1)] = ImageList.LoadImage(imageFiles[i])
EndFor

Timer.Interval = 1000
Timer.Tick = OnTick

startGame:

currentLevel = 1
currentSubLevel = 1

LoadLevelInfo()

lag = 0
frameDelay = 14
framesSinceLastCount = 60
OnTick()

While "True"
  If timeEnded = 1 Then
    Goto endLoop
  EndIf
  
  start = Clock.ElapsedMilliseconds
  
  playerLeft = Shapes.GetLeft(player)
  playerRight = playerLeft + playerWidth
  playerTop = Shapes.GetTop(player)
  playerBottom = playerTop + playerHeight
  
  If playerTop >= Array.GetItemCount(blocks) * blockSize Then
    Shapes.Move(player, -playerWidth, -playerHeight)
    GraphicsWindow.ShowMessage("Падна от света", "Край на играта")
    Goto startGame
  EndIf
  
  hitBlockLeft = 0
  hitBlockRight = 0
  hitBlockTop = 0
  hitBlockBottom = 0
  
  ' Проверка дали се удряш с някой противник
  For i = 1 To Array.GetItemCount(enemyShapes)
    enemyLeft = Shapes.GetLeft(enemyShapes[i])
    enemyRight = enemyLeft + enemies[i]["width"]
    enemyTop = Shapes.GetTop(enemyShapes[i])
    enemyBottom = enemyTop + enemies[i]["height"]
    
    If playerRight >= enemyLeft And playerLeft <= enemyRight And playerTop <= enemyBottom And playerBottom >= enemyTop Then
      If velocity < 0 Then
        velocity = 5
        Shapes.Remove(enemyShapes[i])
        enemies[i]["alive"] = 0
      Else
        Shapes.Move(player, -playerWidth, -playerHeight)
        GraphicsWindow.ShowMessage("Беше убит от противник", "Край на играта")
        Goto startGame
      EndIf
    EndIf
  EndFor
  
  BlockCheck()
  MovePlayer()
  MoveEnemies()
  
  framesSinceLastCount = framesSinceLastCount + 1
  ' Играта върви с 60 fps
  delay = frameDelay - (Clock.ElapsedMilliseconds - start) - lag
  If (delay > 0) Then
    lag = 0
    Program.Delay(delay)
  Else
    lag = lag + frameDelay - (Clock.ElapsedMilliseconds - start)
  EndIf
  
  frames = frames + 1
  If frames = 60 Then
    frames = 0
  EndIf
EndWhile
endLoop:

While restartGame = 0
EndWhile

Goto startGame

Sub OnTick
  If timeEnded = 1 Then
    Goto endTick
  EndIf
  
  levelSeconds = levelSeconds - 1
    
  If levelSeconds = 0 Then
    Shapes.Move(player, -playerWidth, -playerHeight)
    timeEnded = 1
    GraphicsWindow.ShowMessage("Свърши ти времето", "Край на играта")
    restartGame = 1
  EndIf
  
  GraphicsWindow.BrushColor = "Black"
  GraphicsWindow.FillRectangle(0, 0, 100, 30)
  GraphicsWindow.BrushColor = "White"
  GraphicsWindow.DrawText(5, 7, framesSinceLastCount + " fps")
  GraphicsWindow.BrushColor = "Yellow"
  GraphicsWindow.DrawText(55, 2, "Time")
  GraphicsWindow.DrawText(57, 15, levelSeconds)
  framesSinceLastCount = 0
  
  endTick:
EndSub

Sub BlockCheck
  ' Проверяваме дали играча докосва някакъв блок
  For h = 1 To Array.GetItemCount(blocks)
    For w = 1 To Array.GetItemCount(blocks[h])
      If blocks[h][w] <> "0" Then
        blockLeft = Shapes.GetLeft(blockShapes[h][w])
        blockRight = blockLeft + blockSize
        blockTop = Shapes.GetTop(blockShapes[h][w])
        blockBottom = blockTop + blockSize
        ' Ако блока не е на екрана, няма смисъл да го проверявам
        If blockRight > -blockSize And blockLeft < gw + blockSize Then
          ' Провери дали блока се удря
          If playerRight >= blockLeft And playerLeft <= blockRight And playerTop - velocity <= blockBottom And playerBottom + 1 >= blockTop Then
            ' Проверка дали блока се удря от дясно
            If playerLeft > blockRight - moveSpeed And playerLeft <= blockRight And playerTop < blockBottom And playerBottom > blockTop + moveSpeed * 2 Then
              Shapes.Move(player, blockRight, playerTop)
              playerLeft = Shapes.GetLeft(player)
              playerRight = playerLeft + playerWidth
              hitBlockLeft = 1
            ' Проверка дали блока се удря от ляво
            ElseIf playerRight >= blockLeft And playerRight < blockLeft + moveSpeed And playerTop < blockBottom And playerBottom > blockTop Then
              Shapes.Move(player, blockLeft - playerWidth, playerTop)
              playerLeft = Shapes.GetLeft(player)
              playerRight = playerLeft + playerWidth
              hitBlockRight = 1
            EndIf
            ' Провери дали блока се удря от долу
            If playerTop > blockBottom - moveSpeed * 2 And playerTop - 1 < blockBottom And playerRight > blockLeft And playerLeft < blockRight Then
              Shapes.Move(player, playerLeft, blockBottom - 1)
              playerTop = Shapes.GetTop(player)
              playerBottom = playerTop + playerHeight
              hitBlockTop = 1
              If inAir = 1 Then
                velocity = -1
              EndIf
            EndIf
            ' Проверка дали блока се удря от горе
            If playerBottom > blockTop - 2 And playerBottom < blockTop + moveSpeed * 2 And playerRight > blockLeft And playerLeft < blockRight Then
              Shapes.Move(player, playerLeft, blockTop - playerHeight)
              playerTop = Shapes.GetTop(player)
              playerBottom = playerTop + playerHeight
              hitBlockBottom = 1
              SetNotInAir()
              velocity = 0
            EndIf
          EndIf
        EndIf
      EndIf
    EndFor
  EndFor
EndSub

Sub MovePlayer
  playerLeft = Shapes.GetLeft(player)
  playerRight = playerLeft + blockSize
  playerTop = Shapes.GetTop(player)
  playerBottom = playerTop + blockSize
  
  ' Провери дали пада играча, ако не във въздуха вече
  If hitBlockBottom = 0 And inAir = 0 Then
    SetInAir()
    velocity = -1
  EndIf
  
  If keyboard["Up"] = 1 And inAir = 0 And hitBlockTop = 0 Then
    SetInAir()
    velocity = exceleration
  EndIf
  
  ' Премести го ако пада
  If inAir = 1 Then
    If keyboard["Left"] = 1 Then
      If lastPlayerSprite <> 6 Then
        lastPlayerSprite = 6
        Shapes.Remove(player)
        player = Shapes.AddImage(playerInAirLeft)
      EndIf
    ElseIf lastPlayerSprite <> 5 Then
      lastPlayerSprite = 5
      Shapes.Remove(player)
      player = Shapes.AddImage(playerInAirRight)
    EndIf
    Shapes.Move(player, playerLeft, playerTop - velocity)
    playerTop = Shapes.GetTop(player)
    playerBottom = playerTop + playerHeight
    
    If velocity >= -moveSpeed Then
      velocity = velocity - 0.5
    EndIf
  EndIf
  
  ' Премести ако се движи на дясто
  If keyboard["Right"] = 1 And hitBlockRight = 0 Then
    If lastMovedX <> 1 Then
      movmentExceleration = 1
    EndIf
    lastMovedX = 1
    
    If movmentExceleration <= moveSpeed Then
      movmentExceleration = movmentExceleration + 0.2
    EndIf
    
    If inAir = 0 And (Math.Remainder(frames, 8) = 0 Or (lastPlayerSprite <> 3 And lastPlayerSprite <> 4)) Then
      lastPlayerSprite = 3
      Shapes.Remove(player)
      If walkingAnimationType = 0 Then
        player = Shapes.AddImage(playerWalkingRight)
        walkingAnimationType = 1
      Else
        player = Shapes.AddImage(playerWalkingRight2)
        walkingAnimationType = 0
      EndIf
      Shapes.Move(player, playerLeft, playerTop)
    EndIf
    
    MoveLevelRight()
  ' Премести ако се движи на ляво
  ElseIf keyboard["Left"] = 1 And hitBlockLeft = 0 Then
    If lastMovedX <> 2 Then
      movmentExceleration = 1
    EndIf
    lastMovedX = 2
    
    If movmentExceleration <= moveSpeed Then
      movmentExceleration = movmentExceleration + 0.2
    EndIf
    
    If inAir = 0 And (Math.Remainder(frames, 8) = 0 Or (lastPlayerSprite <> 3 And lastPlayerSprite <> 4)) Then
      lastPlayerSprite = 4
      Shapes.Remove(player)
      If walkingAnimationType = 0 Then
        player = Shapes.AddImage(playerWalkingLeft)
        walkingAnimationType = 1
      Else
        player = Shapes.AddImage(playerWalkingLeft2)
        walkingAnimationType = 0
      EndIf
      Shapes.Move(player, playerLeft, playerTop)
    EndIf
    
    MoveLevelLeft()
  ' Ако не се движи в нито една посока тогава му премахни натрупаната бързина
  Else
    movmentExceleration = 1
  EndIf
EndSub

Sub MoveLevelRight
  If position >= cameraLeftMax - moveSpeed * 2 And position <= cameraRightMax And playerLeft > gw / 2 - playerWidth / 2 Then
    ' Премести всички блокове
    For h = 1 To Array.GetItemCount(blocks)
      For w = 1 To Array.GetItemCount(blocks[h])
        If blocks[h][w] <> "0" Then
          If w * blockSize - blockSize < position - blockSize Then
            Shapes.Remove(blockShapes[h][w])
          ElseIf w * blockSize - blockSize > position + gw And w * blockSize - blockSize < position + gw + blockSize Then
            blockShapes[h][w] = Shapes.AddImage(tiles[blocks[h][w]])
            Shapes.Move(blockShapes[h][w], w * blockSize - blockSize - position - movmentExceleration, h * blockSize - blockSize - cameraTopMax)
          Else
            blockTop = Shapes.GetTop(blockShapes[h][w])
            Shapes.Move(blockShapes[h][w], w * blockSize - blockSize - position - movmentExceleration, blockTop)
          EndIf
        EndIf
      EndFor
    EndFor
    ' Премести всички противници
    For i = 1 To Array.GetItemCount(enemyShapes)
      enemyLeft = Shapes.GetLeft(enemyShapes[i])
      enemyTop = Shapes.GetTop(enemyShapes[i])
      Shapes.Move(enemyShapes[i], enemyLeft - movmentExceleration, enemyTop)
    EndFor
    position = position + movmentExceleration
  Else
    Shapes.Move(player, playerLeft + movmentExceleration, playerTop)
  EndIf
EndSub

Sub MoveLevelLeft
  If position >= cameraLeftMax And position <= cameraRightMax + moveSpeed * 2 And playerLeft < gw / 2 + playerWidth / 2 Then
    ' Премести всички блокове
    For h = 1 To Array.GetItemCount(blocks)
      For w = 1 To Array.GetItemCount(blocks[h])
        If blocks[h][w] <> "0" Then
          If w * blockSize - blockSize > position + gw Then
            Shapes.Remove(blockShapes[h][w])
          ElseIf w * blockSize - blockSize < position - blockSize And w * blockSize - blockSize > position - (blockSize * 2) Then
            blockShapes[h][w] = Shapes.AddImage(tiles[blocks[h][w]])
            Shapes.Move(blockShapes[h][w], w * blockSize - blockSize - position + movmentExceleration, h * blockSize - blockSize - cameraTopMax)
          Else
            blockTop = Shapes.GetTop(blockShapes[h][w])
            Shapes.Move(blockShapes[h][w], w * blockSize - blockSize - position + movmentExceleration, blockTop)
          EndIf
        EndIf
      EndFor
    EndFor
    ' Премести всички противници
    For i = 1 To Array.GetItemCount(enemyShapes)
      enemyLeft = Shapes.GetLeft(enemyShapes[i])
      enemyTop = Shapes.GetTop(enemyShapes[i])
      Shapes.Move(enemyShapes[i], enemyLeft + movmentExceleration, enemyTop)
    EndFor
    position = position - movmentExceleration
  Else
    Shapes.Move(player, playerLeft - movmentExceleration, playerTop)
  EndIf
EndSub

Sub MoveEnemies
  For i = 1 To Array.GetItemCount(enemyShapes)
    If enemies[i]["alive"] = 0 Then
      Goto endOfEnemyLoop
    EndIf
    
    enemyLeft = Shapes.GetLeft(enemyShapes[i])
    enemyRight = enemyLeft + enemies[i]["width"]
    
    If enemyRight < 0 Or enemyLeft > gw Then
      Goto endOfEnemyLoop
    EndIf
    
    enemyTop = Shapes.GetTop(enemyShapes[i])
    enemyBottom = enemyTop + enemies[i]["height"]
    
    If enemyTop >= Array.GetItemCount(blocks) * blockSize Then
      Shapes.Remove(enemyShapes[i])
      enemies[i]["alive"] = 0
    EndIf
    
    enemyHitBlockBottom = 0
    
    ' Проверяваме дали противника докосва някакъв блок
    For h = 1 To Array.GetItemCount(blocks)
      For w = 1 To Array.GetItemCount(blocks[h])
        If blocks[h][w] <> "0" Then
          blockLeft = w * blockSize - blockSize - position
          blockRight = blockLeft + blockSize
          blockTop = h * blockSize - blockSize - cameraTopMax
          blockBottom = blockTop + blockSize
          If blockRight > -blockSize And blockLeft < gw + blockSize Then
            ' Провери дали блока се удря
            If enemyRight >= blockLeft And enemyLeft <= blockRight And enemyTop <= blockBottom And enemyBottom + 1 > blockTop Then
              ' Проверка дали блока се удря от дясно
              If ((enemyRight >= blockLeft And enemyRight < blockLeft + 5) Or (enemyLeft > blockRight - 5 And enemyLeft <= blockRight)) And enemyTop + 1 < blockBottom And enemyBottom > blockTop Then
                If enemies[i]["facing"] = 1 Then
                  enemies[i]["facing"] = 0
                Else
                  enemies[i]["facing"] = 1
                EndIf
              EndIf
              
              ' Проверка дали блока се удря от горе
              If enemyBottom > blockTop - 1 And enemyBottom < blockTop + 1 And enemyRight > blockLeft And enemyLeft < blockRight Then
                enemies[i]["velocity"] = 0
                enemyHitBlockBottom = 1
              EndIf
            EndIf
          EndIf
        EndIf
      EndFor
    EndFor
    
    If enemyHitBlockBottom = 0 Then
      enemies[i]["velocity"] = -1
    EndIf
    
    If Math.Remainder(frames, 10) = 0 Then
      Shapes.Remove(enemyShapes[i])
      If enemies[i]["animation"] = 1 Then
        enemies[i]["animation"] = 0
        enemyShapes[i] = Shapes.AddImage(enemy1_2)
      ElseIf enemies[i]["animation"] = 0 Then
        enemies[i]["animation"] = 1
        enemyShapes[i] = Shapes.AddImage(enemy1_1)
      EndIf
    EndIf
    
    If enemies[i]["facing"] = 1 Then
      Shapes.Move(enemyShapes[i], enemyLeft + 1, enemyTop - enemies[i]["velocity"])
    Else
      Shapes.Move(enemyShapes[i], enemyLeft - 1, enemyTop - enemies[i]["velocity"])
    EndIf
    
    endOfEnemyLoop:
  EndFor
EndSub

Sub SetInAir
  inAir = 1
  
  ' Променяме картинката на играча да е във въздуха
  If keyboard["Left"] = 1 Then
    If lastPlayerSprite <> 6 Then
      lastPlayerSprite = 6
      playerLeft = Shapes.GetLeft(player)
      playerTop = Shapes.GetTop(player)
      Shapes.Remove(player)
      player = Shapes.AddImage(playerInAirLeft)
      Shapes.Move(player, playerLeft, playerTop)
    EndIf
  ElseIf lastPlayerSprite <> 5 Then
    lastPlayerSprite = 5
    playerLeft = Shapes.GetLeft(player)
    playerTop = Shapes.GetTop(player)
    Shapes.Remove(player)
    player = Shapes.AddImage(playerInAirRight)
    Shapes.Move(player, playerLeft, playerTop)
  EndIf
EndSub

Sub SetNotInAir
  inAir = 0
  
  ' Променяме картинката на играча да е на земята
  If keyboard["Right"] = 0 And lastMovedX = 1 Then
    If lastPlayerSprite <> 1 Then
      lastPlayerSprite = 1
      playerLeft = Shapes.GetLeft(player)
      playerTop = Shapes.GetTop(player)
      Shapes.Remove(player)
      player = Shapes.AddImage(playerIdleRight)
      Shapes.Move(player, playerLeft, playerTop)
    EndIf
  ElseIf keyboard["Left"] = 0 And lastMovedX = 2 Then
    If lastPlayerSprite <> 2 Then
      lastPlayerSprite = 2
      playerLeft = Shapes.GetLeft(player)
      playerTop = Shapes.GetTop(player)
      Shapes.Remove(player)
      player = Shapes.AddImage(playerIdleLeft)
      Shapes.Move(player, playerLeft, playerTop)
    EndIf
  EndIf
EndSub

Sub ReadLevelFile
  i = 1
  levelFilePath = levelFolderPath + currentLevel + "-" + currentSubLevel + ".txt"
  line = File.ReadLine(levelFilePath, i)
  scanType = 1
  While line <> ""
    While line = "///"
      scanType = scanType + 1
      i = i + 1
      line = File.ReadLine(levelFilePath, i)
    EndWhile
    
    ' Взимаме информацията за блоковете в нивото
    If scanType = 1 Then
      While line <> ""
        val = Text.GetSubText(line, 1, 1)
        line = Text.GetSubTextToEnd(line, 2)
        itemCount = Array.GetItemCount(blocks[i]) + 1
        blocks[i][itemCount] = val
      EndWhile
    ' Взимаме информацията за противниците в нивото
    ElseIf scanType = 2 Then
      itemCount = Array.GetItemCount(enemies) + 1
    
      indexOf = Text.GetIndexOf(line, " ")
      val = Text.GetSubText(line, 1, indexOf)
      line = Text.GetSubTextToEnd(line, indexOf + 1)
      enemies[itemCount]["top"] = val
      
      indexOf = Text.GetIndexOf(line, " ")
      val = Text.GetSubText(line, 1, indexOf)
      line = Text.GetSubTextToEnd(line, indexOf + 1)
      enemies[itemCount]["left"] = val
      
      indexOf = Text.GetIndexOf(line, " ")
      val = Text.GetSubText(line, 1, indexOf)
      line = Text.GetSubTextToEnd(line, indexOf + 1)
      enemies[itemCount]["facing"] = val
      
      enemies[itemCount]["type"] = line
    ' Взимаме информацията за до къде може камерата да стигне
    ElseIf scanType = 3 Then
      indexOf = Text.GetIndexOf(line, " ")
      val = Text.GetSubText(line, 1, indexOf)
      line = Text.GetSubTextToEnd(line, indexOf + 1)
      cameraLeftMax = val * blockSize
      
      indexOf = Text.GetIndexOf(line, " ")
      val = Text.GetSubText(line, 1, indexOf)
      line = Text.GetSubTextToEnd(line, indexOf + 1)
      cameraRightMax = (Array.GetItemCount(blocks[1]) - val) * blockSize
      
      cameraTopMax = line * blockSize
    ' Взимаме информацията за къде играча да се появи
    ElseIf scanType = 4 Then
      indexOf = Text.GetIndexOf(line, " ")
      val = Text.GetSubText(line, 1, indexOf)
      line = Text.GetSubTextToEnd(line, indexOf + 1)
      playerStartingPositionTop = val * blockSize
      
      playerStartingPositionLeft = line * blockSize
    ' Взимаме информацията за времето което имаш да преключиш нивото в секунди
    ElseIf scanType = 5 Then
      levelSeconds = line
    EndIf
    
    i = i + 1
    line = File.ReadLine(levelFilePath, i)
  EndWhile
EndSub

Sub LoadFirstScreen
  ' Зареди снимката от зад
  backgroundOverworld = ImageList.LoadImage(Program.Directory + "\backgrounds\background_overworld.jpg")
  GraphicsWindow.DrawResizedImage(backgroundOverworld, 0, 0, gw, gh)
  
  position = cameraLeftMax
  
  ' Зареди блоковете
  For h = 1 To Array.GetItemCount(blocks)
    For w = 1 To Array.GetItemCount(blocks[h])
      If position - blockSize < w * blockSize - blockSize And position + gw + blockSize > w * blockSize - blockSize Then
        If blocks[h][w] <> "0" Then
          blockShapes[h][w] = Shapes.AddImage(tiles[blocks[h][w]])
          Shapes.Move(blockShapes[h][w], w * blockSize - blockSize - position, h * blockSize - blockSize - cameraTopMax)
        EndIf
      EndIf
    EndFor
  EndFor
  
  ' Постави противниците
  For i = 1 To Array.GetItemCount(enemies)
    If enemies[i]["type"] = 1 Then
      enemyShapes[i] = Shapes.AddImage(enemy1_1)
      enemies[i]["width"] = blockSize
      enemies[i]["height"] = blockSize
    EndIf
    Shapes.Move(enemyShapes[i], enemies[i]["left"] * blockSize - blockSize + 1, enemies[i]["top"] * blockSize - blockSize - 1)
    enemies[i]["velocity"] = 0
    enemies[i]["animation"] = 0
    enemies[i]["alive"] = 1
  EndFor
EndSub

Sub DrawPlayer
  player = Shapes.AddImage(playerIdleRight)
  Shapes.Move(player, playerStartingPositionLeft - blockSize, playerStartingPositionTop - 1 - blockSize)
EndSub

Sub OnKeyDown
  If GraphicsWindow.LastKey = "Left" Or GraphicsWindow.LastKey = "A" Then
    keyboard["Left"] = 1
  ElseIf GraphicsWindow.LastKey = "Right" Or GraphicsWindow.LastKey = "D" Then
    keyboard["Right"] = 1
  ElseIf GraphicsWindow.LastKey = "Up" Or GraphicsWindow.LastKey = "W" Or GraphicsWindow.LastKey = "Space" Then
    keyboard["Up"] = 1
  EndIf
EndSub

Sub OnKeyUp
  If GraphicsWindow.LastKey = "Left" Or GraphicsWindow.LastKey = "A" Then
    keyboard["Left"] = 0
  ElseIf GraphicsWindow.LastKey = "Right" Or GraphicsWindow.LastKey = "D" Then
    keyboard["Right"] = 0
  ElseIf GraphicsWindow.LastKey = "Up" Or GraphicsWindow.LastKey = "W" Or GraphicsWindow.LastKey = "Space" Then
    keyboard["Up"] = 0
  EndIf
EndSub

Sub LoadLevelInfo
  GraphicsWindow.BrushColor = "White"
  GraphicsWindow.Clear()
  blocks = ""
  enemies = ""
  enemyShapes = ""
  blockShapes = ""
  player = ""
  timeEnded = 0
  restartGame = 0
  cameraLeftMax = 0
  cameraRightMax = 0
  levelSeconds = 0
  velocity = 0
  inAir = 0
  position = 0
  movmentExceleration = 0
  lastMovedX = 1
  frames = 0
  walkingAnimationType = 1
  hitBlockLeft = 0
  hitBlockRight = 0
  hitBlockTop = 0
  hitBlockBottom = 0
  lastPlayerSprite = 1

  ReadLevelFile()
  LoadFirstScreen()
  DrawPlayer()
EndSub